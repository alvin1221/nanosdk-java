plugins {
    id 'com.github.johnrengelman.shadow' version '6.1.0' apply false
    id 'java'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

group 'io.sisu'
version '1.3.2-SNAPSHOT'

repositories {
    mavenCentral()
}
test {
    useJUnitPlatform()
}
dependencies {
    implementation 'net.java.dev.jna:jna:5.6.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}

project(':demo') {
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'java'

    group = 'io.sisu.nng'
    version = parent.version

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation rootProject
    }

    jar {
        manifest {
            attributes 'Description': 'Some demonstrations of NNG-Java'
        }
    }

    task demoFatJar(type: Jar) {
        from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
        baseName 'nng-demo-uberjar'
        with jar
    }

    task runDemoAsyncServer(type: JavaExec) {
        group = "Execution"
        description = "Run the Async Server demo"
        classpath = sourceSets.main.runtimeClasspath
        main = $/io.sisu.nng.demo.async.Server/$
        environment "JNA_LIBRARY_PATH", System.getenv("JNA_LIBRARY_PATH")
    }

    task runDemoAsyncClient(type: JavaExec) {
        group = "Execution"
        description = "Run the Async Client demo"
        classpath = sourceSets.main.runtimeClasspath
        main = $/io.sisu.nng.demo.async.Client/$
        environment "JNA_LIBRARY_PATH", System.getenv("JNA_LIBRARY_PATH")
    }
}